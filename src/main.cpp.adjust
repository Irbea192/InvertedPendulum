#include <M5Unified.h>
#include <esp_now.h>

#include <WiFi.h>

String mymacAddress = "08:F9:E0:D2:55:B8";

typedef struct {
  String deviceStatus = "";
  String functionStatus = "";
  String sendStatus = "";
} LcdData;


LcdData lcdData;

typedef struct {
  int minPwm = 0;
  int antiWindup =200;
  float targetAngle = -3.9;
  float Kp = 30.0;
  float Ki = 600.0;
  float Kd = 1.5;
  float motorGainA = 0.97;
  float motorGainB = 1.0;
} cData;

cData dataToSend;
cData oldData;

int incPwm = 1;
int incAntiWindup = 10;
float incTargetAngle = 0.1;
float increaseMotorGainA = 0.01;
float increaseMotorGainB = 0.01;
float increaseKp = 1.0;
float increaseKi = 5.0;
float increaseKd = 0.01;

uint8_t targetAddress[] = {0xB8, 0xF8, 0x62, 0xF9, 0xC4, 0xD4};
esp_now_peer_info_t peerInfo;

int const maxMode = 9;
#define PWM_MODE 0
#define ANGLE_MODE 1
#define ANTI_WINDUP_MODE 2
#define MOTOR_GAIN_MODE_A 3
#define MOTOR_GAIN_MODE_B 4
#define KP_MODE 5
#define KI_MODE 6
#define KD_MODE 7
#define SEND_MODE 8
int mode = 0;
String modeNames[maxMode] = {"PWM", "Ang", "AWU", "MGA", "MGB", "Kp", "Ki", "Kd", "Send"};
String aBtn[maxMode] = {"-PWM", "-Ang", "-AWU", "-MGA", "+MGB", "-Kp", "-Ki", "-Kd", ""};
String cBtn[maxMode] = {"+PWM", "+Ang", "+AWU", "-MGA", "+MGB", "+Kp", "+Ki", "+Kd", "Send"};

int btnHeight = 220; // ボタン表示のY座標
int btnAPos = 60; // ボタンAの中心位置
int btnBPos = 155; // ボタンBの中心位置
int btnCPos= 285; // ボタンCの中心位置

void setupLcd() {
  M5.Lcd.clear();
  M5.Lcd.fillScreen(BLACK);
  M5.Lcd.setTextColor(WHITE);
  M5.Lcd.setTextSize(2);
}

void errLcd() {
  M5.Lcd.clear();
  M5.Lcd.setCursor(10, 10);
  M5.Lcd.printf(lcdData.deviceStatus.c_str());
}

void printfLcd() {
  M5.Lcd.clear();
  M5.Lcd.setCursor(10, 10);
  M5.Lcd.printf("F Status: %s", lcdData.functionStatus.c_str());
  M5.Lcd.setCursor(10, 30);
  M5.Lcd.printf("S Status: %s", lcdData.sendStatus.c_str());
  M5.Lcd.setCursor(10, 50);
  M5.Lcd.printf("Mode: %s", modeNames[mode].c_str());

  M5.Lcd.setCursor(10, 70);
  M5.Lcd.printf("PWM:%d TA:%.1f AWU:%d", oldData.minPwm, oldData.targetAngle, oldData.antiWindup);
  M5.Lcd.setCursor(10, 90);
  M5.Lcd.printf("PWM:%d TA:%.1f AWU:%d", dataToSend.minPwm, dataToSend.targetAngle, dataToSend.antiWindup);
  M5.Lcd.setCursor(10, 110);
  M5.Lcd.printf("MGA:%.2f MGB:%.2f", oldData.motorGainA, oldData.motorGainB);
  M5.Lcd.setCursor(10, 130);
  M5.Lcd.printf("MGA:%.2f MGB:%.2f", dataToSend.motorGainA, dataToSend.motorGainB);
  
  M5.Lcd.setCursor(30, 150);
  M5.Lcd.printf(" Kp   Ki   Kd");
  M5.Lcd.setCursor(30, 170);
  M5.Lcd.printf(" %.2f  %.2f  %.2f", oldData.Kp, oldData.Ki, oldData.Kd);
  M5.Lcd.setCursor(30, 190);
  M5.Lcd.printf(" %.2f  %.2f  %.2f", dataToSend.Kp, dataToSend.Ki, dataToSend.Kd);
  M5.Lcd.setCursor(30, 210);
  M5.Lcd.printf("+-%.1f  +-%.1f  +-%.1f", increaseKp, increaseKi, increaseKd);

  // ボタン表示
  M5.Lcd.setCursor(btnBPos - 20, btnHeight);
  M5.Lcd.printf("Mode");

  M5.Lcd.setCursor(btnAPos - 30, btnHeight);
  M5.Lcd.printf(aBtn[mode].c_str());

  M5.Lcd.setCursor(btnCPos - 30, btnHeight);
  M5.Lcd.printf(cBtn[mode].c_str());
}

void OnDataSent(const uint8_t *mac_addr, esp_now_send_status_t status) {
  Serial.print("送信ステータス: ");
  Serial.println(status == ESP_NOW_SEND_SUCCESS ? "成功" : "失敗");
  lcdData.sendStatus = (status == ESP_NOW_SEND_SUCCESS) ? "Success" : "Fail";
  memcpy(&oldData, &dataToSend, sizeof(cData));
}

void sendData() {
  esp_err_t result = esp_now_send(targetAddress, (uint8_t *)&dataToSend, sizeof(dataToSend));
  if (result == ESP_OK) {
    Serial.println("データ送信成功");
    lcdData.functionStatus = "Success";
  } else {
    Serial.println("データ送信失敗");
    lcdData.functionStatus = "Fail";
  }
}

void setup() {
  auto cfg = M5.config();
  cfg.clear_display = true;
  M5.begin(cfg);
  setupLcd();

  Serial.begin(115200);


  WiFi.mode(WIFI_STA);
  if (esp_now_init() != ESP_OK) {
    Serial.println("Failed to initialize ESP-NOW");
    lcdData.deviceStatus = "Failed to initialize ESP-NOW";
    errLcd();
    return;
  }

  memcpy(peerInfo.peer_addr, targetAddress, 6);
  peerInfo.channel = 0;
  peerInfo.encrypt = false;

  if (esp_now_add_peer(&peerInfo) != ESP_OK) {
    Serial.println("Failed to add peer");
    lcdData.deviceStatus = "Failed to add peer";
    errLcd();
    return;
  }

  esp_now_register_send_cb(OnDataSent);

  Serial.println("Setup done!");
  lcdData.deviceStatus = "Setup done!";
  printfLcd();
}

void loop() {
  M5.update();
  if (M5.BtnA.wasPressed()) {
    switch (mode){
      case PWM_MODE:
        dataToSend.minPwm -= incPwm;
        dataToSend.minPwm = constrain(dataToSend.minPwm, 0, 255);
        break;

      case ANGLE_MODE:
        dataToSend.targetAngle -= incTargetAngle;
        dataToSend.targetAngle = constrain(dataToSend.targetAngle, -90.0, 90.0);
        break;

      case ANTI_WINDUP_MODE:
        dataToSend.antiWindup -= incAntiWindup;
        dataToSend.antiWindup = constrain(dataToSend.antiWindup, 0, 10000);
        break;
      
      case MOTOR_GAIN_MODE_A:
        dataToSend.motorGainA -= increaseMotorGainA;
        dataToSend.motorGainA = constrain(dataToSend.motorGainA, 0.0, 1.0);
        break;
      
      case MOTOR_GAIN_MODE_B:
        dataToSend.motorGainB -= increaseMotorGainB;
        dataToSend.motorGainB = constrain(dataToSend.motorGainB, 0.0, 1.0);
        break; 

      case KP_MODE:
        dataToSend.Kp -= increaseKp;
        dataToSend.Kp = constrain(dataToSend.Kp, 0.0, 10000.0);
        break;

      case KI_MODE:
        dataToSend.Ki -= increaseKi;
        dataToSend.Ki = constrain(dataToSend.Ki, 0.0, 10000.0);
        break;

      case KD_MODE:
        dataToSend.Kd -= increaseKd;
        dataToSend.Kd = constrain(dataToSend.Kd, 0.0, 10000.0);
        break;

      default:
        break;
    }
    printfLcd();
  }
  if (M5.BtnB.wasPressed()) {
    mode = (mode + 1) % maxMode;
    printfLcd();
  }
  if (M5.BtnC.wasPressed()) {
    switch (mode){
      case PWM_MODE:
        dataToSend.minPwm += incPwm;
        dataToSend.minPwm = constrain(dataToSend.minPwm, 0, 255);
        break;

      case ANGLE_MODE:
        dataToSend.targetAngle += incTargetAngle;
        dataToSend.targetAngle = constrain(dataToSend.targetAngle, -90.0, 90.0);
        break;
      
      case ANTI_WINDUP_MODE:
        dataToSend.antiWindup += 10;
        dataToSend.antiWindup = constrain(dataToSend.antiWindup, 0, 10000);
        break;

      case MOTOR_GAIN_MODE_A:
        dataToSend.motorGainA += increaseMotorGainA;
        dataToSend.motorGainA = constrain(dataToSend.motorGainA, 0.0, 1.0);
        break;

      case MOTOR_GAIN_MODE_B:
        dataToSend.motorGainB += increaseMotorGainB;
        dataToSend.motorGainB = constrain(dataToSend.motorGainB, 0.0, 1.0);
        break;

      case KP_MODE:
        dataToSend.Kp += increaseKp;
        break;

      case KI_MODE:
        dataToSend.Ki += increaseKi;
        break;

      case KD_MODE:
        dataToSend.Kd += increaseKd;
        break;
      
      case SEND_MODE:
        sendData();
        break;

      default:
        break;
    }
    printfLcd();
  }
}